//@version=6
indicator("Impulse Trend Arrows", shorttitle="Impulse Trend Arrows", overlay=true)

//------------------------
// User Inputs 
//------------------------
basisLen      = input.int(50, "Adaptive Baseline Length")
showBands     = input.bool(true, "Show ATR Bands")
showBg        = input.bool(true, "Background Shading")
showArrows    = input.bool(true, "Show Entry Arrows")

// Visual options
smoothLen     = input.int(8, "Channel Smoothing", minval=1) // chanel smotther, the more the smoother!
baseColorIn   = input.color(color.new(#5f5f5f, 0), "Baseline Color")
upColorIn     = input.color(color.new(#000000, 0), "Upper Band Color")
dnColorIn     = input.color(color.new(#000000, 0), "Lower Band Color")
fillColorIn   = input.color(color.new(color.blue, 75), "Channel Fill Color")


atrLen   = 14
atrMult  = 2.0
rsiLen   = 14
macdFast = 12
macdSlow = 26
macdSig  = 9

// Main Logic

basis   = ta.ema(close, basisLen)
atr     = ta.atr(atrLen)
upBand  = basis + atrMult * atr
dnBand  = basis - atrMult * atr

impulseUp = close > basis and close > upBand
impulseDn = close < basis and close < dnBand

rsi       = ta.rsi(close, rsiLen)
macdLine  = ta.ema(close, macdFast) - ta.ema(close, macdSlow)
macdSigLn = ta.ema(macdLine, macdSig)
macdHist  = macdLine - macdSigLn

momUp = rsi > 50 and macdHist > 0
momDn = rsi < 50 and macdHist < 0

longCond  = impulseUp and momUp
shortCond = impulseDn and momDn

var string lastSignal = "NONE"
newBuy  = longCond  and lastSignal != "BUY"
newSell = shortCond and lastSignal != "SELL"

if newBuy
    lastSignal := "BUY"
else if newSell
    lastSignal := "SELL"


basisSmoothed  = ta.ema(basis, smoothLen)
upBandSmoothed = ta.ema(upBand, smoothLen)
dnBandSmoothed = ta.ema(dnBand, smoothLen)

plot(basisSmoothed, "Baseline", baseColorIn, 3)

pUp = plot(showBands ? upBandSmoothed : na, "ATR Upper", upColorIn)
pDn = plot(showBands ? dnBandSmoothed : na, "ATR Lower", dnColorIn)

fill(pUp, pDn, fillColorIn)

//------------------------
// Arrows shifted OUTSIDE channel
//------------------------
arrowOffsetFactor = 2.0   // how far outside the channel (2x ATR below/above)
textOffsetFactor  = 0.8   // distance between arrow and text

buyArrowY  = dnBandSmoothed - atr * arrowOffsetFactor
buyTextY   = buyArrowY     - atr * textOffsetFactor

sellArrowY = upBandSmoothed + atr * arrowOffsetFactor
sellTextY  = sellArrowY    + atr * textOffsetFactor

if showArrows and newBuy
    // Arrow below channel
    label.new(
         bar_index, buyArrowY,
         text      = "",
         style     = label.style_triangleup,
         color     = color.new(color.green, 0),
         textcolor = color.white,
         size      = size.large)
    // BUY text under the arrow
    label.new(
         bar_index, buyTextY,
         text      = "BUY",
         style     = label.style_label_center,
         color     = color.new(color.green, 85),
         textcolor = color.new(#6186ff, 0),
         size      = size.small)

if showArrows and newSell
    // Arrow above channel
    label.new(
         bar_index, sellArrowY,
         text      = "",
         style     = label.style_triangledown,
         color     = color.new(color.red, 0),
         textcolor = color.white,
         size      = size.large)
    // SELL text above the arrow
    label.new(
         bar_index, sellTextY,
         text      = "SELL",
         style     = label.style_label_center,
         color     = color.new(color.red, 85),
         textcolor = color.new(#6186ff, 0),
         size      = size.small)

//------------------------
// Background + TOP panel
//------------------------
neutralColor = color.new(color.gray, 82)
bgColorUp    = color.new(color.green, 65)
bgColorDn    = color.new(color.red, 65)

bgcolor(showBg ? (lastSignal == "BUY" ? bgColorUp : lastSignal == "SELL" ? bgColorDn : neutralColor) : na)

var table panel = table.new(position.top_center, 1, 3, border_width=1)
trend = lastSignal == "BUY" ? 1 : lastSignal == "SELL" ? -1 : 0
trendText  = trend == 1 ? "UPTREND" : trend == -1 ? "DOWNTREND" : "NEUTRAL"
trendColor = trend == 1 ? color.rgb(1, 126, 66) : trend == -1 ? color.rgb(200, 0, 0) : color.gray

if barstate.islast
    table.cell(panel, 0, 0, "CURRENT TREND DIRECTION", text_color=color.white, bgcolor=color.black, width=27)
    table.cell(panel, 0, 1, trendText, text_color=color.white, bgcolor=trendColor, width=25)
